plugins {
  id "java"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.jk1.dependency-license-report" version "0.3.11"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
	mavenLocal()
  jcenter()
}

dependencies {
  compile 'com.marklogic:ml-javaclient-util:' + mlJavaclientUtilVersion
  compile 'jaxen:jaxen:1.1.6'
  compile 'org.apache.httpcomponents:httpclient:4.3.6'
  compile 'org.springframework:spring-web:4.3.5.RELEASE'

	// For EqualsBuilder; added in 3.8.1 to support detecting if a mimetype's properties have changed or not
	compile "org.apache.commons:commons-lang3:3.7"

	// For PreviewInterceptor; can be excluded if that feature is not used
	compile "com.flipkart.zjsonpatch:zjsonpatch:0.4.6"

	// Don't want to include this in the published jar, just the executable jar
	compileOnly "com.beust:jcommander:1.72"

  testCompile 'com.marklogic:ml-junit:' + mlJunitVersion
  testCompile 'commons-io:commons-io:2.5'

  // Forcing Spring to use logback instead of commons-logging
  compile "ch.qos.logback:logback-classic:1.1.8" // Needs to be compile for CLI
  runtime group: "org.slf4j", name: "jcl-over-slf4j", version: "1.7.22"
  runtime group: "org.slf4j", name: "slf4j-api", version: "1.7.22"
}

// This ensures that Gradle includes in the published jar any non-java files under src/main/java
sourceSets.main.resources.srcDir 'src/main/java'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from file(javadocsDir + "/" + project.name)
}

javadoc.destinationDir = file(javadocsDir + "/" + project.name)
javadoc.failOnError = false

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
	  mavenJavadoc(MavenPublication) {
		  artifact javadocJar
	  }
  }
}

if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}

test {
  testLogging {
    events 'started','passed', 'skipped', 'failed'
    exceptionFormat 'full'
  }
}

task executableJar(type: Jar) {
	baseName = "deployer"
	manifest {
		attributes("Main-Class": "com.marklogic.appdeployer.cli.Main")
	}
	// Include this project's class files
	from sourceSets.main.output
	// Include all project dependencies
	from {
		configurations.runtime.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	from {
		configurations.compileOnly.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}
