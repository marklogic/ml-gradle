buildscript {
  repositories {
    maven { 
      url "https://plugins.gradle.org/m2/" 
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.2"
  }
}

plugins {
  id "groovy"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.5"
}

apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile mlAppDeployerDependency
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources' 
  from sourceSets.main.allJava
  from sourceSets.main.allGroovy
}

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
  repositories {
    maven {
      url publishUrl
    }
  }
}
 
if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = "2.0"
        released = new Date()
      }
    }
  }
}

pluginBundle {
  website = 'http://www.gradle.org/'
  vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
  description = 'Testing ml-gradle publishing'
  tags = ['marklogic']
  
  plugins {
    mlgradlePlugin {
      id = 'com.marklogic.ml-gradle'
      displayName = 'ml-gradle for MarkLogic'
      description = 'Gradle plugin for configuring and deploying applications to MarkLogic'
      tags = ['marklogic']
      version = "2.0"
    }
  }

  mavenCoordinates {
    version = "2.0"
  }
}