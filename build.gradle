buildscript {
  repositories {
		jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
		classpath "com.smokejumperit.gradle.license:Gradle-License-Report:0.0.2"
  }
}

plugins {
  id "groovy"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.6"
  id "com.github.jk1.dependency-license-report" version "0.3.11"
  id "com.gradle.plugin-publish" version "0.9.7"
}

apply plugin: "com.gradle.plugin-publish"

// gradle dependencyLicenseReport to generate your report in build/reports/dependency-license
apply plugin:'license-report'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
  mavenLocal() // Used for local development only
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile mlAppDeployerDependency
  compile mlcpUtilDependency
  compile group: 'commons-io', name: 'commons-io', version: '2.5'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
  from sourceSets.main.allGroovy
}

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}

if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}

pluginBundle {
  website = 'http://www.gradle.org/'
  vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
  description = 'Publishes ml-gradle'
  tags = ['marklogic']

  plugins {
    mlgradlePlugin {
      id = 'com.marklogic.ml-gradle'
      displayName = 'ml-gradle for MarkLogic'
      description = 'Gradle plugin for configuring and deploying applications to MarkLogic'
      tags = ['marklogic']
      version = "2.9.0"
    }
  }

  mavenCoordinates {
    version = "2.9.0"
  }
}
