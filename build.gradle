plugins {
  id "groovy"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.jk1.dependency-license-report" version "0.3.11"
  id "com.gradle.plugin-publish" version "0.9.7"
	id "java-gradle-plugin"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
  mavenLocal() // Used for local development only
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile mlAppDeployerDependency
  compile mlcpUtilDependency
	compile mlDataMovementDependency
  compile group: 'commons-io', name: 'commons-io', version: '2.5'

	compileOnly mlUnitTestDependency

	testCompile localGroovy()
	testCompile gradleTestKit()
	testCompile 'xmlunit:xmlunit:1.3'
	testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
		exclude module: 'groovy-all'
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
  from sourceSets.main.allGroovy
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from file(javadocsDir + "/" + project.name)
}

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
	  mavenJavadoc(MavenPublication) {
		  artifact javadocJar
	  }
  }
}

if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}

pluginBundle {
  website = 'http://www.gradle.org/'
  vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
  description = 'Publishes ml-gradle'
  tags = ['marklogic']

  plugins {
    mlgradlePlugin {
      id = 'com.marklogic.ml-gradle'
      displayName = 'ml-gradle for MarkLogic'
      description = 'Gradle plugin for configuring and deploying applications to MarkLogic'
      tags = ['marklogic']
      version = project.version
    }
  }

  mavenCoordinates {
    version = project.version
  }
}
