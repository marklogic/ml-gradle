plugins {
  id "groovy"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.jk1.dependency-license-report" version "1.3"
  id "com.gradle.plugin-publish" version "0.11.0"
	id "java-gradle-plugin"

	id "net.saliman.properties" version "1.5.1"
	id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

sourceCompatibility = "8"
targetCompatibility = "8"

repositories {
  mavenLocal() // Used for local development only
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.marklogic:ml-app-deployer:4.1.0"
  compile "com.marklogic:mlcp-util:0.9.0"
	compile "com.marklogic:marklogic-data-movement-components:2.2.0"
	compile "commons-io:commons-io:2.8.0"

	compileOnly "com.marklogic:marklogic-unit-test-client:1.0.0"

	testCompile localGroovy()
	testCompile gradleTestKit()
	testCompile 'xmlunit:xmlunit:1.3'

	// Force spock-core to use 4.13.1 to avoid security issue in 4.11
	testCompile 'junit:junit:4.13.1'
	testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
		exclude module: 'groovy-all'
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
  from sourceSets.main.allGroovy
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from file(javadocsDir + "/" + project.name)
}

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
	  mavenJavadoc(MavenPublication) {
		  artifact javadocJar
	  }
  }
}

if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}

pluginBundle {
  website = 'https://www.marklogic.com/'
  vcsUrl = 'https://github.com/marklogic-community/' + project.name + '.git'
  description = 'Publishes ml-gradle'
  tags = ['marklogic']

  plugins {
    mlgradlePlugin {
      id = 'com.marklogic.ml-gradle'
      displayName = 'ml-gradle for MarkLogic'
      description = 'Gradle plugin for configuring and deploying applications to MarkLogic'
      tags = ['marklogic']
      version = project.version
    }
  }

  mavenCoordinates {
    version = project.version
  }
}

// See https://github.com/snyk/gradle-plugin for more information
snyk {
	//arguments = '--all-sub-projects'
	severity = 'low'
	api = snykToken
	autoDownload = true
	autoUpdate = true
}
