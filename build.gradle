plugins {
  id "java"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.5"
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
  jcenter()
  mavenLocal()
}

dependencies {
  compile 'com.marklogic:ml-javaclient-util:2.9.0'
  compile 'jaxen:jaxen:1.1.6'
  compile 'org.apache.httpcomponents:httpclient:4.3.5'
  compile 'org.springframework:spring-web:4.1.5.RELEASE'
  testCompile ('com.marklogic:ml-junit:2.6.0') {
    exclude module: "ml-javaclient-util"
  }
}

// This ensures that Gradle includes in the published jar any non-java files under src/main/java
sourceSets.main.resources.srcDir 'src/main/java'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources' 
  from sourceSets.main.allJava
}

javadoc.destinationDir = file(javadocsDir + "/" + project.name)

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}
 
if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = "2.2.0"
        released = new Date()
      }
    }
  }
}



task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}
