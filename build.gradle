// Defines common configuration for each of the 3 subprojects.
subprojects {
	apply plugin: "java-library"
	apply plugin: "maven-publish"

	group = "com.marklogic"
	version = "6.0-SNAPSHOT"

	java {
		sourceCompatibility = 17
		targetCompatibility = 17
	}

	repositories {
		mavenCentral()
		mavenLocal()
		maven {
			url "https://bed-artifactory.bedford.progress.com:443/artifactory/ml-maven-snapshots/"
		}
	}

	dependencies {
		// Without this, once using JUnit 5.12 or higher, Gradle will not find any tests and report an error of:
		// org.junit.platform.commons.JUnitException: TestEngine with ID 'junit-jupiter' failed to discover tests
		testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.13.4"

		testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
		testImplementation "org.springframework:spring-test:${springVersion}"

		// Forcing Spring to use logback instead of commons-logging
		testImplementation "ch.qos.logback:logback-classic:1.5.18"
		testImplementation 'org.slf4j:jcl-over-slf4j:2.0.17'
		testImplementation 'org.slf4j:slf4j-api:2.0.17'
	}

	javadoc.failOnError = false
	// Ignores warnings on params that don't have descriptions, which is a little too noisy
	javadoc.options.addStringOption('Xdoclint:none', '-quiet')

	test {
		useJUnitPlatform()
		testLogging {
			events 'started','passed', 'skipped', 'failed'
			exceptionFormat 'full'
		}
	}

	// Publishing setup - see https://docs.gradle.org/current/userguide/publishing_setup.html .
	java {
		withJavadocJar()
		withSourcesJar()
	}

	publishing {
		publications {
			mainJava(MavenPublication) {
				groupId = group
				artifactId = project.name
				version = version
				from components.java
				pom {
					name = "${group}:${project.name}"
					description = "${projectDescription}"
					packaging = "jar"
					url = "https://github.com/marklogic/${project.name}"
					licenses {
						license {
							name = "The Apache License, Version 2.0"
							url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
						}
					}
					developers {
						developer {
							id = "marklogic"
							name = "MarkLogic Github Contributors"
							email = "general@developer.marklogic.com"
							organization = "MarkLogic"
							organizationUrl = "https://www.marklogic.com"
						}
					}
					scm {
						url = "git@github.com:marklogic/ml-gradle.git"
						connection = "scm:git@github.com:marklogic/ml-gradle.git"
						developerConnection = "scm:git@github.com:marklogic/ml-gradle.git"
					}
				}
			}
		}
		repositories {
			maven {
				if (project.hasProperty("mavenUser")) {
					credentials {
						username mavenUser
						password mavenPassword
					}
					url publishUrl
					allowInsecureProtocol = true
				} else {
					name = "central"
					url = mavenCentralUrl
					credentials {
						username mavenCentralUsername
						password mavenCentralPassword
					}
				}
			}
		}
	}
}
