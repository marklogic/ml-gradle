plugins {
	id "java-library"
	id "maven-publish"
	id "signing"
	id "io.snyk.gradle.plugin.snykplugin" version "0.4"

	// This plugin requires Java 11; keeping it commented out so that this project can be built and tested via
	// Java 8, 11 or 17; to use this plugin, uncomment this line and use java 11 or higher
	//	id "com.github.jk1.dependency-license-report" version "2.1"

	// For deploying the test app.
	id 'net.saliman.properties' version '1.5.2'
	id "com.marklogic.ml-gradle" version "4.5.2"
}

group = "com.marklogic"
version = "4.6-SNAPSHOT"

java {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

repositories {
	mavenCentral()
}

dependencies {
	api 'com.marklogic:marklogic-client-api:6.2.0'
	api 'com.marklogic:marklogic-xcc:11.0.2'
	api 'org.springframework:spring-context:5.3.27'

	implementation 'org.jdom:jdom2:2.0.6.1'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'

	// This is currently an implementation dependency, though perhaps should be an api dependency due to its usage in
	// LoggingObject; not clear yet on whether the protected Logger in that class should make this an api dependency or not
	implementation 'org.slf4j:slf4j-api:1.7.36'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
	testImplementation 'org.springframework:spring-test:5.3.27'
	testImplementation 'org.mockito:mockito-core:4.11.0'

	// Used for testing loading modules from the classpath
	testImplementation files("lib/modules.jar")

	// Forcing Spring to use logback instead of commons-logging
	testImplementation "ch.qos.logback:logback-classic:1.3.5" // Not using 1.4.x yet as it requires Java 11
	testImplementation "org.slf4j:jcl-over-slf4j:1.7.36"
	testImplementation "org.slf4j:slf4j-api:1.7.36"

	// For verifying that JAXB can be used in a test on Java 11 or higher
	testImplementation "javax.xml.bind:jaxb-api:2.3.1"
	testImplementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"
	testImplementation "org.glassfish.jaxb:jaxb-core:2.3.0.1"

	// Including the "new" JAXB libraries to verify that their presence doesn't cause the "old" JAXB libraries to fail
	testImplementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.0"
	testImplementation "com.sun.xml.bind:jaxb-impl:4.0.1"
}

test {
	useJUnitPlatform()
	testLogging {
		events 'started', 'passed', 'skipped', 'failed'
		exceptionFormat 'full'
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from javadoc
}
javadoc.failOnError = false
// Ignores warnings on params that don't have descriptions, which is a little too noisy
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

artifacts {
	archives javadocJar, sourcesJar
}
signing {
	sign configurations.archives
}

task generatePomForDependencyGraph(dependsOn: "generatePomFileForMainJavaPublication") {
	description = "Prepare for a release by making a copy of the generated pom file in the root directory so that it " +
		"can enable Github's Dependency Graph feature, which does not yet support Gradle"
	doLast {
		def preamble = '<?xml version="1.0" encoding="UTF-8"?>'
		def comment = "<!--\n" +
			"This file was generated via Gradle and is being used primarily for github's Dependency Graph feature.\n" +
			"It is not intended to be used to build this project.\n" +
			"-->"
		def fileText = file("build/publications/mainJava/pom-default.xml").getText()
		file("pom.xml").setText(fileText.replace(preamble, preamble + comment))
	}
}

publishing {
	publications {
		mainJava(MavenPublication) {
			pom {
				name = "${group}:${project.name}"
				description = "Library that adds functionality on top of the MarkLogic Java Client"
				packaging = "jar"
				url = "https://github.com/marklogic-community/${project.name}"
				licenses {
					license {
						name = "The Apache License, Version 2.0"
						url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}
				developers {
					developer {
						id = "marklogic"
						name = "MarkLogic Github Contributors"
						email = "general@developer.marklogic.com"
						organization = "MarkLogic"
						organizationUrl = "https://www.marklogic.com"
					}
				}
				scm {
					url = "git@github.com:marklogic-community/${project.name}.git"
					connection = "scm:git@github.com:marklogic-community/${project.name}.git"
					developerConnection = "scm:git@github.com:marklogic-community/${project.name}.git"
				}
			}
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
	repositories {
		maven {
			if (project.hasProperty("mavenUser")) {
				credentials {
					username mavenUser
					password mavenPassword
				}
				url publishUrl
			} else {
				name = "central"
				url = mavenCentralUrl
				credentials {
					username mavenCentralUsername
					password mavenCentralPassword
				}
			}
		}
	}
}


// See https://github.com/snyk/gradle-plugin for more information
snyk {
	//arguments = '--all-sub-projects'
	severity = 'low'
	api = snykToken
	autoDownload = true
	autoUpdate = true
}
