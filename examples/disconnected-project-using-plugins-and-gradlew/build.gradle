buildscript {
  repositories {
		// To use gradle in disconnected mode, you just need to set the 'disconnected' property. E.g. gradle compileJava -Pdisconnected
		if (project.hasProperty("disconnected") && !"FALSE".equalsIgnoreCase(disconnected)) {
			println "Using offline buildscript dependency repositories"
			maven { url uri(projectMavenRepo) }
		} else {
			println "Using online buildscript dependency repositories"
			mavenCentral()
		}
	}
}

plugins {
	id 'java' //optional - delete if not needed
	id 'net.saliman.properties' version '1.5.1'
	id 'com.marklogic.ml-gradle' version "4.5.1"
}

repositories {
	// To use gradle in disconnected mode, you just need to set the 'disconnected' property. E.g. gradle compileJava -Pdisconnected
	if (project.hasProperty("disconnected") && !"FALSE".equalsIgnoreCase(disconnected)) {
		println "Using offline dependency repositories"
		maven { url uri(projectMavenRepo) }
	} else {
		println "Using online dependency repositories"
		mavenCentral()
	}
}

configurations {
	mlcp //example if you want to use mlcp. Delete otherwise
}

dependencies {
	//sample mlcp dependency. Remove if not required
	mlcp "com.marklogic:mlcp:10.0.6.2"
}

/**
 * START: Disconnected gradle tasks
 */
gradle.taskGraph.whenReady { graph ->
	if (graph.hasTask(downloadToProjectMavenRepo)) {
		println project.gradle.gradleUserHomeDir
		if (!project.gradle.gradleUserHomeDir.equals(new File(rootDir, projectGradleHome))) {
			throw new GradleException("Please set the gradle user home property to $projectGradleHome on the gradle command line - e.g. \n " +
				(System.getProperty("os.name").startsWith("Windows") ? "" : "./") +
				"gradlew -Dgradle.user.home=$projectGradleHome <task_to_execute>")
		}
	}
}

task downloadToProjectMavenRepo(type: Copy) {
	/*
	 * Include any configuration dependencies here that you want to copy the dependencies for.
	 * These are defined in the 'dependencies' block. E.g. you need to include
	 * configurations.implementation.files if you want your java 'implementation' dependencies downloaded
	 */
	configurations.mlcp.files //includes 'mlcp' dependencies. Remove if not needed

	from new File(gradle.gradleUserHomeDir, 'caches/modules-2/files-2.1') // correct as of gradle 4.7
	into new File(rootDir, projectMavenRepo)
	eachFile {
		List<String> parts = it.path.split('/')
		it.path = (parts[0].replace('.', '/') + '/' + parts[1]) + '/' + parts[2] + '/' + parts[4]
	}
	includeEmptyDirs false
}

task makeOfflineZip(type: Zip, dependsOn: downloadToProjectMavenRepo) {
	from rootDir
	excludes = ['.tmp', '.gradle', 'build/gradle-home', 'build/distributions', 'build/offline/gradle/wrapper/dists']
	destinationDir(file('build/distributions'))
	archiveName = 'offline.zip'
	doLast {
		println "Created offline project zip at build/distributions/offline.zip"
	}
}

/**
 * END: Disconnected gradle tasks
 */
